configurations {
	querydslApt.extendsFrom implementation, api
}

ext.querydslGeneratedSource = file("$buildDir/generated-sources")

sourceSets {
	main.java.srcDirs querydslGeneratedSource
}

dependencies {
	compile 'org.hibernate:hibernate-core:5.4.1.Final'
	compile 'com.querydsl:querydsl-jpa:4.2.1'
	compile 'javax.annotation:javax.annotation-api:1.3.2'
	querydslApt 'com.querydsl:querydsl-apt:4.2.1'
}

task generateQuerydsl(type: JavaCompile, group: 'build') {
	description = 'Generates the QueryDSL query types (Q-classes)'

	source = sourceSets.main.java
	classpath = configurations.querydslApt
	options.annotationProcessorPath = classpath
	options.compilerArgs = [
		'-proc:only',
		'-processor', 'com.querydsl.apt.jpa.JPAAnnotationProcessor'
	]
	destinationDir = querydslGeneratedSource
}


compileJava {
	dependsOn generateQuerydsl
	source generateQuerydsl.destinationDir
}